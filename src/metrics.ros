# imports
:global getTimestamp;
:global nrMetricsToJson;
:global NrApiKey;

:local timestamp [$getTimestamp];
:local NrApiKey $NrApiKey;

:local toMetric do={
    :ret {"name"=$name; "value"=$value; "type"="gauge"; "timestamp"=$timestamp};
}

:local observedMetrics {
    "mikrotic.system.cpu.load"=[/system resource get cpu-load];
    "mikrotic.system.memory.total"=[/system resource get total-memory];
    "mikrotic.system.memory.free"=[/system resource get free-memory];

    "mikrotic.firewall.connection.tcp"=[/ip firewall connection print count-only where protocol=tcp];
    "mikrotic.firewall.connection.udp"=[/ip firewall connection print count-only where protocol=udp];
    "mikrotic.firewall.connection.established"=[/ip firewall connection print count-only where tcp-state=established];

    "mikrotic.ip.dns.cache.size"=[/ip dns get cache-size];
    "mikrotic.ip.dns.cache.used"=[/ip dns get cache-used];
    "mikrotic.ip.pool.used"=[/ip pool used print count-only];
    "mikrotic.ip.dhcpserver.leases"=[/ip dhcp-server lease print active count-only];
};


:local metricsArray [:toarray ""];
:foreach k,v in=$observedMetrics do={
    :local metric [$toMetric name=$k value=$v timestamp=$timestamp];
    :set $metricsArray ($metricsArray , {$metric});
}

:local httpData [$nrMetricsToJson metrics=$metricsArray];

/tool fetch http-method=post output=none http-header-field="Content-Type:application/json,Api-Key:$NrApiKey" http-data=$httpData url="https://metric-api.eu.newrelic.com/metric/v1"
